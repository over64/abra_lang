import
  /universe with None, Bool, Int, String
  /range with Range
  /io .

type Node = (left: Node | None, right: Node | None)

def asNode = self: Node | None, callback: (Node) -> None do llvm
  %1 = getelementptr { void (%"Node"*, i8*)*, i8* }, { void (%"Node"*, i8*)*, i8* }* %callback, i64 0, i32 0
  %fn = load void (%"Node"*, i8*)*, void (%"Node"*, i8*)** %1
  %2 = bitcast { void (%"Node"*, i8*)*, i8* }* %callback to i8*
  %3 = load %Node*, %Node** %self
  call void %fn(%Node* %3, i8* %2)
  ret void .None

def mkTree = depth: Int do
  if depth > 0 do
    Node(mkTree(depth - 1), mkTree(depth - 1))
  else
    Node(none, none) ..Node

def checksum = self: Node do
  sum = 1

  when self.left  is l: Node do
     self.right asNode lambda r: Node ->
       sum = sum + l.checksum() + r.checksum() .

  sum .Int

def stat = nTrees: Int, name: String, depth: Int, sum: Int do
  if nTrees > 1 do io.printInt(nTrees) .
  io.print(name) io.printInt(depth) io.print('\t check: ') io.printInt(sum) io.print('\n') .

def main =
  minDepth = 4
  maxDepth = 21 # sys.arg(0).toInt()

  if maxDepth < minDepth + 2 do
    maxDepth = minDepth + 2 .

  tree = mkTree(maxDepth + 1)
  stat(1, 'stretch tree of depth ', maxDepth + 1, tree.checksum())

  llTree = mkTree(maxDepth)
  stat(1, 'long lived tree of depth ', maxDepth, llTree.checksum())

  Range(minDepth, maxDepth, 2) each lambda depth: Int ->
    iters    = 1 shl (maxDepth + minDepth - depth)
    totalSum = 0

    Range(1, iters + 1, 1) each lambda i: Int ->
      totalSum = totalSum + mkTree(depth).checksum() .

    stat(iters, '\t trees of depth ', depth, totalSum) .

  0 .